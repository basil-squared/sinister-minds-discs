import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {

	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "2.3.0"
}


version = project.findProperty("version") ?: project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {

}

loom {
	splitEnvironmentSourceSets()

	mods {
		"sinister-discs" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

fabricApi {
	configureDataGeneration {
		client = true
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
}

processResources {
	inputs.property "version", project.version
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions {
		jvmTarget.set(JvmTarget.JVM_21)
	}
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}
tasks.named('sourcesJar') {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.named('remapJar') {

	def baseName = project.property("archives_base_name")
	def mcVersion = project.property("minecraft_version")


	archiveFileName = "${baseName}-${project.version}-FABRIC-${mcVersion}.jar"
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.getProperties()["archives_base_name"]
			from components.java
		}
	}
	repositories {
	}
}